/*
--
--  File: dbatrg_trace_ts.sql
--  Date: Fri May  4 09:48:22 CEST 2018
--  What: enable dba logon trigger to set a trace a specific schema
          which is enabled through the system.dba$trace_schemas table
--
*/

set verify off

-- :: argv: none, trace logon trigger for schemas in TRACE_SCHEMAS

set serveroutput on

create or replace trigger dba$after_logon_schema
after logon
on database
declare
   instancenm varchar2(30);
begin
   select instance_name into instancenm from v$instance; 
  
   for trace in (
      select schemaname
      ,      osuser
      from   dba$trace_schemas
      where  enabled = 'Y' )
      loop
         for ssn in (
            select  s.sid
            ,       s.serial#
            ,       s.osuser
            ,       s.username
            ,       s.machine
            ,       p.spid
            from    v$session s
            join    v$process p
            on      p.addr   = s.paddr 
            where   s.audsid = userenv('SESSIONID')
            and     s.osuser like trace.osuser
            and     s.username = trace.schemaname )
        loop
           sys.dbms_system.set_sql_trace_in_session(ssn.sid,ssn.serial#,true);
        -- sys.dbms_system.set_ev(ssn.sid,ssn.serial#,10046,8,'');  /* waits */
           sys.dbms_system.set_ev(ssn.sid,ssn.serial#,10046,12,''); /* waits and binds */
           insert into dba$traced_sessions 
              ( ts_id
              , ts_timestamp
              , osuser
              , schemaname
              , session_id
              , serial#
              , trace_file_short
              , server_name
      	      , comments ) values
              ( dba$traced_sessions_seq.nextval
              , current_timestamp
              , ssn.osuser
              , ssn.username
              , ssn.sid
              , ssn.serial#
              , instancenm ||'_ora_'|| ssn.spid ||'.trc' 
              , ssn.machine
              , 'Generated by dba$after_logon_schema trigger' );
         end loop;
      end loop;
exception
   when others then null;
end;
/

